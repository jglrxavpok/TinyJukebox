plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.21'
}

group 'org.jglrxavpok.tinyjukebox'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    jcenter()
    maven {
        url "https://www.beatunes.com/repo/maven2/"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // .ogg & .mp3 codecs
    implementation group: 'org.jcraft', name: 'jorbis', version: '0.0.17'
    implementation group: 'com.googlecode.soundlibs', name: 'mp3spi', version: '1.9.5.4'
    implementation group: 'javazoom', name: 'jlayer', version: '1.0.1'
    implementation group: 'com.googlecode.soundlibs', name: 'tritonus-share', version: '0.3.7.4'
    implementation group: 'com.googlecode.soundlibs', name: 'vorbisspi', version: '1.0.3.3'
    implementation group: 'com.jcraft', name: 'jogg', version: '0.0.7'

    // Websocket library
    implementation "org.java-websocket:Java-WebSocket:1.4.0"

    // Gson
    implementation 'com.google.code.gson:gson:2.8.5'

    // Kotlin coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.1.1'

    // Exposed (SQL Framework)
    implementation 'org.jetbrains.exposed:exposed:0.14.3'
    
    // JTwig (Templating Engine)
    implementation "org.jtwig:jtwig-core:5.87.0.RELEASE"

    // SQLite Driver
    implementation "org.xerial:sqlite-jdbc:3.21.0.1"

    // JBcrypt
    implementation group: 'org.mindrot', name: 'jbcrypt', version: '0.4'

    // K-Router: Kotlin Router
    implementation project(':k-router')
}

task copyRuntimeLibs(type: Copy) {
    into "build/libs"
    from configurations.runtime
}

jar.dependsOn copyRuntimeLibs

jar {
    manifest {
        attributes(
            'Implementation-Title': 'TinyJukebox',
            'Implementation-Version': version,
            'Main-Class': 'BootstrapKt',
            'Class-Path': configurations.compile.collect { it.getName() }.join(' ')
        )
    }
    baseName = project.name
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task run(type: JavaExec) {
    main = "-jar"
    args "build/libs/tinyjukebox-"+project.version+".jar", "-Xss32M"
}

run.dependsOn build